      SUBROUTINE SIGNETFIC(SFIC,F,MF0,RW,FILPROPS,NETPROPS,
     1                                   RHO,LAMBDA0,N,DET,NDI)
C>    AFFINE NETWORK:  'FICTICIUOUS' CAUCHY STRESS 
      IMPLICIT NONE
      INCLUDE 'PARAM_UMAT.INC'
C
      DOUBLE PRECISION SFIC(NDI,NDI),F(NDI,NDI),MF0(NWP,NDI),
     1                 RW(NWP),SFILFIC(NDI,NDI)
      DOUBLE PRECISION FILPROPS(5),NETPROPS(3),MFI(NDI),MF0I(NDI)
      DOUBLE PRECISION DET,AUX,PI,LAMBDAI,DWI,DDWI,RWI,FI,FFI
      DOUBLE PRECISION L,R0,MU0,B0,BETA,LAMBDA0,RHO,N
      INTEGER NDI,I1,J1,K1
C
C     FILAMENT
      L      = FILPROPS(1)
      R0      = FILPROPS(2)
      MU0     = FILPROPS(3)
      BETA    = FILPROPS(4)
      B0      = FILPROPS(5)
C     NETWORK
      N      = NETPROPS(1)
      LAMBDA0 = NETPROPS(2)
      RHO     = NETPROPS(3)
C
      PI=FOUR*ATAN(ONE)
      AUX=N*(DET**(-ONE))*FOUR*PI
      SFIC=ZERO
C
      DO I1=1,NWP
C
       MFI=ZERO
       DO J1=1,NDI
        MF0I(J1)=MF0(I1,J1)
       END DO
       RWI=RW(I1)

C
       CALL DEFFIL(LAMBDAI,MFI,MF0I,F,NDI)
C
       CALL FIL(FI,FFI,DWI,DDWI,LAMBDAI,LAMBDA0,L,R0,MU0,BETA,B0)
C
       CALL SIGFILFIC(SFILFIC,RHO,LAMBDAI,DWI,MFI,RWI,NDI)
C
       DO J1=1,NDI
        DO K1=1,NDI
         SFIC(J1,K1)=SFIC(J1,K1)+AUX*SFILFIC(J1,K1)
        END DO
       END DO
C
      END DO
C
      RETURN
      END SUBROUTINE SIGNETFIC
